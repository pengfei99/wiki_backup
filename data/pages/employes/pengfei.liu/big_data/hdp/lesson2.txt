====== Lesson2: Handling Semi-structured data ======

In this lesson, we will show you how we handle semi-structured data in HIVE and spark. The data which we use is from IOT system of trucks (e.g. trucks.csv, geolocation.csv). 

You can find these data on my google drive (https://drive.google.com/drive/folders/1XpOxVfnUXYyvhHHavqBgyUJGSiiPJAG0)

===== 1. Load data to hdfs =====

Although hive and spark can work with local fs, but we prefer to use hdfs, which gives us more realistic big data environment context.

<code>
# If you run a sandbox on docker, you need to copy the data to vm
scp -P 2222 /path/to/file root@127.0.0.1:/tmp/

# load data to hdfs
hdfs dfs -mkdir /tmp/data
hdfs dfs -put /path/to/file /tmp/data

</code>

===== 2. Import data to hive =====

Here, we will combine external and managed table. We will explain why in the end of this section

==== 2.1 Load data in raw textfile format ====

<code>
# Create a hive database
$ create database if not exists truck_iot

# note in hive, database and schema are equal

# Create an external table for trucks and geolocation
$ create external table if not exists truck_iot.trucks_raw (driverid string,truckid string,model string,jun13_miles int,jun13_gas int, may13_miles int, may13_gas int, apr13_miles int, apr13_gas int,  
mar13_miles int, mar13_gas int, feb13_miles int, feb13_gas int, jan13_miles int,jan13_gas int, dec12_miles int, dec12_gas int, nov12_miles int, nov12_gas int, oct12_miles int, oct12_gas int,
sep12_miles int,sep12_gas int, aug12_miles int, aug12_gas int, jul12_miles int, jul12_gas int,jun12_miles int,jun12_gas int,may12_miles int,may12_gas int,apr12_miles int, apr12_gas int,
mar12_miles int,mar12_gas int,feb12_miles int,feb12_gas int,jan12_miles int, jan12_gas int, dec11_miles int, dec11_gas int, nov11_miles int,nov11_gas int,oct11_miles int,oct11_gas int,
sep11_miles int, sep11_gas int,aug11_miles int,aug11_gas int,jul11_miles int,jul11_gas int,jun11_miles int,jun11_gas int, may11_miles int, may11_gas int, apr11_miles int, apr11_gas int,
mar11_miles int, mar11_gas int,feb11_miles int,feb11_gas int,jan11_miles int,jan11_gas int, dec10_miles int, dec10_gas int, nov10_miles int, nov10_gas int, oct10_miles int, oct10_gas int,
sep10_miles int, sep10_gas int,aug10_miles int, aug10_gas int,jul10_miles int,jul10_gas int, jun10_miles int, jun10_gas int, may10_miles int, may10_gas int, apr10_miles int, apr10_gas int,
mar10_miles int, mar10_gas int,feb10_miles int,feb10_gas int,jan10_miles int,jan10_gas int, dec09_miles int, dec09_gas int,nov09_miles int,nov09_gas int, oct09_miles int,oct09_gas int,
sep09_miles int, sep09_gas int, aug09_miles int,aug09_gas int,jul09_miles int,jul09_gas int,jun09_miles int, jun09_gas int,may09_miles int,may09_gas int,apr09_miles int,apr09_gas int,
mar09_miles int, mar09_gas int, feb09_miles int,feb09_gas int,jan09_miles int,jan09_gas int) ROW FORMAT DELIMITED FIELDS TERMINATED BY ',' LINES TERMINATED BY '\n' STORED AS TextFile tblproperties("skip.header.line.count"="1");

# load trucks.csv to table trucks_raw
Load data inpath '/tmp/data/trucks.csv' into table truck_iot.trucks_raw;  


# Create table for geolocation
create external table if not exists truck_iot.geolocation_raw (truckid STRING, driverid STRING, event STRING, latitude DOUBLE, longitude DOUBLE, city STRING, state STRING, velocity INT, event_ind INT, idling_ind INT) ROW FORMAT DELIMITED FIELDS TERMINATED BY ',' LINES TERMINATED BY '\n' STORED AS TextFile tblproperties("skip.header.line.count"="1");

# load geolocation.csv to table geolocation_raw
Load data inpath '/tmp/data/geolocation.csv' into table truck_iot.geolocation_raw;
</code>

Now the data has been loaded, let's get familiar with the hive data warehouse

Some useful command to understand the table and database
<code>
# list all database/schema
$ show databases/schemas; 

# show table column;
$ describe geolocation_orc;

# show table columns and all metadata of the table
$ describe formatted geolocation_orc;

# show the commad for table creation
$ show create table geolocation;
</code>

Get Some basic stats and key an eye on the execution time
<code>
# Get the top ten trucks with the highest velocity/speed
select * from truck_iot.geolocation_raw order by velocity desc limit 10;

# Get all event a truck can get
select distinct(event) from truck_iot.geolocation_raw limit 10;
</code>

==== 2.2 Transform the data in orc format ====

The Optimized Row Columnar (ORC) file format provides a highly efficient way to store Hive data. It was designed to overcome limitations of the other Hive file formats. Using ORC files improves performance when Hive is reading, writing, and processing data. 

For more information about orc, check this link: https://cwiki.apache.org/confluence/display/Hive/LanguageManual+ORC

<code>
# Create a managed table with orc format for geolocation
$ create table if not exists truck_iot.geolocation_orc (truckid STRING, driverid STRING, event STRING, latitude DOUBLE, longitude DOUBLE, city STRING, state STRING, velocity INT, event_ind INT, idling_ind INT) STORED AS ORC;

# Create a managed table with orc format for trucks
$ create table if not exists truck_iot.trucks_orc (driverid string,truckid string,model string,jun13_miles int,jun13_gas int, may13_miles int, may13_gas int, apr13_miles int, apr13_gas int,  
mar13_miles int, mar13_gas int, feb13_miles int, feb13_gas int, jan13_miles int,jan13_gas int, dec12_miles int, dec12_gas int, nov12_miles int, nov12_gas int, oct12_miles int, oct12_gas int,
sep12_miles int,sep12_gas int, aug12_miles int, aug12_gas int, jul12_miles int, jul12_gas int,jun12_miles int,jun12_gas int,may12_miles int,may12_gas int,apr12_miles int, apr12_gas int,
mar12_miles int,mar12_gas int,feb12_miles int,feb12_gas int,jan12_miles int, jan12_gas int, dec11_miles int, dec11_gas int, nov11_miles int,nov11_gas int,oct11_miles int,oct11_gas int,
sep11_miles int, sep11_gas int,aug11_miles int,aug11_gas int,jul11_miles int,jul11_gas int,jun11_miles int,jun11_gas int, may11_miles int, may11_gas int, apr11_miles int, apr11_gas int,
mar11_miles int, mar11_gas int,feb11_miles int,feb11_gas int,jan11_miles int,jan11_gas int, dec10_miles int, dec10_gas int, nov10_miles int, nov10_gas int, oct10_miles int, oct10_gas int,
sep10_miles int, sep10_gas int,aug10_miles int, aug10_gas int,jul10_miles int,jul10_gas int, jun10_miles int, jun10_gas int, may10_miles int, may10_gas int, apr10_miles int, apr10_gas int,
mar10_miles int, mar10_gas int,feb10_miles int,feb10_gas int,jan10_miles int,jan10_gas int, dec09_miles int, dec09_gas int,nov09_miles int,nov09_gas int, oct09_miles int,oct09_gas int,
sep09_miles int, sep09_gas int, aug09_miles int,aug09_gas int,jul09_miles int,jul09_gas int,jun09_miles int, jun09_gas int,may09_miles int,may09_gas int,apr09_miles int,apr09_gas int,
mar09_miles int, mar09_gas int, feb09_miles int,feb09_gas int,jan09_miles int,jan09_gas int) STORED AS ORC;

# Load data into table geolocation
$ insert into table truck_iot.geolocation_orc SELECT * FROM truck_iot.geolocation_raw;

# Load data into table trucks
$ insert into table truck_iot.trucks_orc SELECT * FROM truck_iot.trucks_raw;
</code>

Now rerun the basic stats query and check again the execution time

<code>
# Get the top ten trucks with the highest velocity/speed
select * from truck_iot.geolocation_raw order by velocity desc limit 10;

# Get all event a truck can get
select distinct(event) from truck_iot.geolocation_raw limit 10;
</code>

===== 3 Advance analysis with hive =====

The manager wants to know two things:
  * Which driver is the most efficient in driving (miles per gallon)
  * Which truck is the most efficient (miles per gallon) 
  * Which driver did the longest road.

We will start with our truck data table.  We need to sum up all the miles and gas columns on a per truck basis. 

==== 3.1 Understand the data and transform it for analysis ====

<code>

# The following command pivots a column with temporal information into rows with two columns (1. time, 2, value). For example, the column "jun13_miles" with value 9217 will become two columns "date" with value jun13, "miles" with value 9217.

$ CREATE TABLE truck_iot.truckmileage STORED AS ORC AS SELECT truckid, driverid, rdate, miles, gas, miles / gas mpg FROM truck_iot.trucks_orc LATERAL VIEW stack(54, 'jun13',jun13_miles,jun13_gas,
'may13',may13_miles,may13_gas,'apr13',apr13_miles,apr13_gas,'mar13',mar13_miles,mar13_gas,'feb13',feb13_miles,feb13_gas,'jan13',jan13_miles,jan13_gas,'dec12',dec12_miles,
dec12_gas,'nov12',nov12_miles,nov12_gas,'oct12',oct12_miles,oct12_gas,'sep12',sep12_miles,sep12_gas,'aug12',aug12_miles,aug12_gas,'jul12',jul12_miles,jul12_gas,
'jun12',jun12_miles,jun12_gas,'may12',may12_miles,may12_gas,'apr12',apr12_miles,apr12_gas,'mar12',mar12_miles,mar12_gas,'feb12',feb12_miles,feb12_gas,'jan12',jan12_miles,jan12_gas,
'dec11',dec11_miles,dec11_gas,'nov11',nov11_miles,nov11_gas,'oct11',oct11_miles,oct11_gas,'sep11',sep11_miles,sep11_gas,'aug11',aug11_miles,aug11_gas,'jul11',jul11_miles,jul11_gas,
'jun11',jun11_miles,jun11_gas,'may11',may11_miles,may11_gas,'apr11',apr11_miles,apr11_gas,'mar11',mar11_miles,mar11_gas,'feb11',feb11_miles,feb11_gas,'jan11',jan11_miles,jan11_gas,
'dec10',dec10_miles,dec10_gas,'nov10',nov10_miles,nov10_gas,'oct10',oct10_miles,oct10_gas,'sep10',sep10_miles,sep10_gas,'aug10',aug10_miles,aug10_gas,'jul10',jul10_miles,jul10_gas,
'jun10',jun10_miles,jun10_gas,'may10',may10_miles,may10_gas,'apr10',apr10_miles,apr10_gas,'mar10',mar10_miles,mar10_gas,'feb10',feb10_miles,feb10_gas,'jan10',jan10_miles,jan10_gas,
'dec09',dec09_miles,dec09_gas,'nov09',nov09_miles,nov09_gas,'oct09',oct09_miles,oct09_gas,'sep09',sep09_miles,sep09_gas,'aug09',aug09_miles,aug09_gas,'jul09',jul09_miles,jul09_gas,
'jun09',jun09_miles,jun09_gas,'may09',may09_miles,may09_gas,'apr09',apr09_miles,apr09_gas,'mar09',mar09_miles,mar09_gas,'feb09',feb09_miles,feb09_gas,'jan09',jan09_miles,jan09_gas) 
dummyalias AS rdate, miles, gas;

# check the geneated table
select * from truck_iot.truckmileage limit 10;

</code>

==== 3.2 Which driver is the most efficient in driving (miles per gallon)====
We want to calculate the average miles per gallon for all drivers and take the top ten most efficient
<code>
# calculate the average mpg for each truck
SELECT driverid, avg(mpg) avgmpg FROM truck_iot.truckmileage GROUP BY driverid;

# Save the result in a new table
CREATE TABLE truck_iot.avgmileage_drivers STORED AS ORC AS SELECT driverid, avg(mpg) avgmpg FROM truck_iot.truckmileage GROUP BY driverid;

# check the genereated result
select * from truck_iot.avgmileage_drivers order by avgmpg desc limit 10;
</code>

==== 3.3 Which truck is the most efficient (miles per gallon)====

We want to calculate the average miles per gallon for all trucks and take the top ten most efficient
<code>
# calculate the average mpg for each truck
SELECT truckid, avg(mpg) avgmpg FROM truck_iot.truckmileage GROUP BY truckid;

# Save the result in a new table
CREATE TABLE truck_iot.avgmileage_trucks STORED AS ORC AS SELECT truckid, avg(mpg) avgmpg FROM truck_iot.truckmileage GROUP BY truckid;

# check the genereated result
select * from truck_iot.avgmileage_trucks order by avgmpg desc limit 10;
</code>

==== 3.4 Which driver did the longest road. ====

<code>
# Create Table DriverMileage from Existing truckmileage data
$ CREATE TABLE truck_iot.DriverMileage STORED AS ORC AS SELECT driverid, sum(miles) totmiles FROM truck_iot.truckmileage GROUP BY driverid;

# 
$ select * from truck_iot.DriverMileage order by totmiles DESC limit 10; 
</code>
===== Appendix =====

==== A.1 Alter external table name ====

You must have noticed, we can't change the external table name. To alter the name, we need to first convert the external table to managed table

<code>
ALTER TABLE truck_iot.geolocation SET TBLPROPERTIES('EXTERNAL'='FALSE');
ALTER TABLE truck_iot.geolocation RENAME TO truck_iot.geolocation_raw;
ALTER TABLE truck_iot.geolocation_raw SET TBLPROPERTIES('EXTERNAL'='TRUE');
</code>

==== A.2 Hive supported data storage formats ====

Hive supports TextFile, SequenceFile, RCFile, AVRO, ORC and Parquet as data storage format.

  * TextFile: is the default format, Data is stored in lines, with each line being a record. Each line is terminated by a newline character (\n).
  * Sequence files: are Hadoop flat files which stores values in binary key-value pairs.
  * RCFile: is row columnar file format. This is another form of Hive file format which offers high row-level compression rates.
  * AVRO: is open source project that provides data serialization and data exchange services for Hadoop. You can exchange data between Hadoop eco-system and program written in any programming languages
  * ORC: stands for Optimized Row Columnar file format. provides a highly efficient way to store data in the Hive table. This file system was actually designed to overcome limitations of the other Hive file formats. The Use of ORC files improves performance when Hive is reading, writing, and processing data from large tables.
  * Parquet: is a column-oriented binary file format. The parquet is highly efficient for the types of large-scale queries. Parquet is especially good for queries scanning particular columns within a particular table

==== A.3 Database and Schema in hive ====

Database and schema in a hive server are the same things, Hive will create a new directory in the hive warehouse path(HDFS)to store all table information of the databases or schemas.

===== Common Errors =====

==== 1. Failed to load csv files to the tables stored as orc ====

If we create a table with stored as orc, we **can not** load csv file directly into it. To load csv, we must create a table stored as TextFile. Then use 
<code>
$ Load data inpath 'path/to/csv' into table text_table;

$ insert into table orc_table SELECT * from text_table;
</code>


